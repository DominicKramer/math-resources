Vectors in Sage are built, manipulated and interrogated in much the same way as matrices (see \acronymref{sage}{M}).  However as simple lists (``one-dimensional,'' not ``two-dimensional'' such as matrices that look more tabular) they are simpler to construct and manipulate.  Sage will print a vector across the screen, even if we wish to interpret it as a column vector.  It will be delimited by parentheses (\verb?(,)?) which allows us to distinguish a vector from a matrix with just one row, if we look carefully.  The number of ``slots'' in a vector is not referred to in Sage as rows or columns, but rather by ``degree.''  Here are some examples (remember to start counting from zero):
%
\begin{sageexample}
sage: v = vector(QQ, 4, [1, 1/2, 1/3, 1/4])
sage: v
(1, 1/2, 1/3, 1/4)
sage: v.degree()
4
sage: v.parent()
Vector space of dimension 4 over Rational Field
sage: v[2]
1/3
\end{sageexample}
%
\begin{sageexample}
sage: w = vector([1, 2, 3, 4, 5, 6])
sage: w
(1, 2, 3, 4, 5, 6)
sage: w.degree()
6
sage: w.parent()
Ambient free module of rank 6 over
the principal ideal domain Integer Ring
sage: w[3]
4
\end{sageexample}
%
Notice that if you use commands like \verb?.parent()? you will sometimes see references to ``free modules.''  This is a technical generalization of the notion of a vector, which is beyond the scope of this course, so just mentally convert to vectors when you see this term.\par
%
The zero vector is super easy to build, but be sure to specify what number system your zero is from.
%
\begin{sageexample}
sage: z = zero_vector(QQ, 5)
sage: z
(0, 0, 0, 0, 0)
\end{sageexample}
%
Notice that while using Sage, we try to remain consistent with our mathematical notation conventions.  This is not required, as you can give items in Sage very long names if you wish.  For example, the following is perfectly legitimate, as you can see.
%
\begin{sageexample}
sage: blatzo = matrix(QQ, 2, [1, 2, 3, 4])
sage: blatzo
[1 2]
[3 4]
\end{sageexample}
%
In fact, our use of capital letters for matrices actually contradicts some of the conventions for naming objects in Sage, so there are good reasons for \emph{not} mirroring our mathematical notation.
%
\begin{sageverbatim}
\end{sageverbatim}
