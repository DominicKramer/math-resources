Sage defines our set $M_{mn}$ as a ``matrix space'' with the command \texttt{MatrixSpace(R, m, n)} where \verb?R? is a number system and \verb?m? and \verb?n? are the number of rows and number of columns, respectively.  This object does not have much functionality defined in Sage.  Its main purposes are to provide a parent for matrices, and to provide another way to create matrices.  The two matrix operations just defined (addition and scalar multiplication) are implemented as you would expect.  In the example below, we create two matrices in $M_{2,3}$ from just a list of 6 entries, by coercing the list into a matrix by using the relevant matrix space as if it were a function.  Then we can perform the basic operations of matrix addition (\acronymref{definition}{MA}) and matrix scalar multiplication (\acronymref{definition}{MSM}).
%
%
\begin{sageexample}
sage: MS = MatrixSpace(QQ, 2, 3)
sage: MS
Full MatrixSpace of 2 by 3 dense matrices over Rational Field
sage: A = MS([1, 2, 1, 4, 5, 4])
sage: B = MS([1/1, 1/2, 1/3, 1/4, 1/5, 1/6])
sage: A + B
[   2  5/2  4/3]
[17/4 26/5 25/6]
sage: 60*B
[60 30 20]
[15 12 10]
sage: 5*A - 120*B
[-115  -50  -35]
[ -10    1    0]
\end{sageexample}
%
Coercion can make some interesting conveniences possible.  Notice how the scalar \verb?37? in the following expression is coerced to $37$ times an identity matrix of the proper size.
%
\begin{sageexample}
sage: A = matrix(QQ, [[ 0,  2, 4],
...                   [ 6,  0, 8],
...                   [10, 12, 0]])
sage: A + 37
[37  2  4]
[ 6 37  8]
[10 12 37]
\end{sageexample}
%
This coercion only applies to sums with \emph{square} matrices.  You might try this again, but with a rectangular matrix, just to see what the error message says.
%
\begin{sageverbatim}
\end{sageverbatim}
%
