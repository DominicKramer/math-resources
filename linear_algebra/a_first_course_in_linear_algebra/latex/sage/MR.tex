It is very easy to create matrix representations of linear transformations in Sage.  Our examples in the text have used abstract vector spaces to make it a little easier to keep track of the domain and codomain.  With Sage we will have to consistently use variants of \verb?QQ^n?, but we will use non-obvious bases to make it nontrivial and interesting.  Here are the components of our first example, one linear function, two vector spaces, four bases.
%
\begin{sageexample}
sage: x1, x2, x3, x4 = var('x1, x2, x3, x4')
sage: outputs = [3*x1 + 7*x2 + x3 - 4*x4,
...              2*x1 + 5*x2 + x3 - 3*x4,
...               -x1 - 2*x2      +   x4]
sage: T_symbolic(x1, x2, x3, x4) = outputs

sage: U = QQ^4
sage: V = QQ^3

sage: b0 = vector(QQ, [ 1, 1, -1, 0])
sage: b1 = vector(QQ, [-1, 0, -2, 7])
sage: b2 = vector(QQ, [ 0, 1, -2, 4])
sage: b3 = vector(QQ, [-2, 0, -1, 6])
sage: B = [b0, b1, b2, b3]

sage: c0 = vector(QQ, [ 1,  6, -6])
sage: c1 = vector(QQ, [ 0,  1, -1])
sage: c2 = vector(QQ, [-2, -3,  4])
sage: C = [c0, c1, c2]

sage: d0 = vector(QQ, [ 1, -3,  2, -1])
sage: d1 = vector(QQ, [ 0,  1,  0,  1])
sage: d2 = vector(QQ, [-1,  2, -1, -1])
sage: d3 = vector(QQ, [ 2, -8,  4, -3])
sage: D = [d0, d1, d2, d3]

sage: e0 = vector(QQ, [ 0,  1, -3])
sage: e1 = vector(QQ, [-1,  2, -1])
sage: e2 = vector(QQ, [ 2, -4,  3])
sage: E = [e0, e1, e2]

\end{sageexample}
%
We create alternate versions of the domain and codomain by providing them with bases other than the standard ones.  Then we build the linear transformation and ask for its \verb?.matrix()?.  We will use numerals to distinguish our two examples.
%
\begin{sageexample}
sage: U1 = U.subspace_with_basis(B)
sage: V1 = V.subspace_with_basis(C)
sage: T1 = linear_transformation(U1, V1, T_symbolic)
sage: T1.matrix(side='right')
[ 15 -67 -25 -61]
[-75 326 120 298]
[  3 -17  -7 -15]
\end{sageexample}
%
Now, we do it again, but with the bases \verb?D? and \verb?E?.
%
\begin{sageexample}
sage: U2 = U.subspace_with_basis(D)
sage: V2 = V.subspace_with_basis(E)
sage: T2 = linear_transformation(U2, V2, T_symbolic)
sage: T2.matrix(side='right')
[ -32    8   38  -91]
[-148   37  178 -422]
[ -80   20   96 -228]
\end{sageexample}
%
So once the pieces are in place, it is quite easy to obtain a matrix representation.  You might experiment with examples from the text, by first mentally converting elements of abstract vector spaces into column vectors via vector representations using simple bases for the abstract spaces.\par
%
The linear transformations \verb?T1? and \verb?T2? are not different as functions, despite the fact that Sage treats them as unequal (since they have different matrix representations).  We can check that the two functions behave identically, first with random testing.  Repeated execution of the following compute cell should always produce \verb?True?.
%
\begin{sageexample}
sage: u = random_vector(QQ, 4)
sage: T1(u) == T2(u)
True
\end{sageexample}
%
A better approach would be to see if  \verb?T1? and \verb?T2? agree on a basis for the domain, and to avoid any favoritism, we will use the basis of \verb?U? itself.  Convince yourself that this is a proper application of \acronymref{theorem}{LTDB} to demonstrate equality.
%
\begin{sageexample}
sage: all([T1(u) == T2(u) for u in U.basis()])
True
\end{sageexample}
%
Or we can just ask if they are equal functions (a method that is implemented using the previous idea about agreeing on a basis).
%
\begin{sageexample}
sage: T1.is_equal_function(T2)
True
\end{sageexample}
%
We can also demonstrate \acronymref{theorem}{FTMR} --- we will use the representation for \verb?T1?.  We need a couple of vector representation linear transformations.
%
\begin{sageexample}
sage: rhoB = linear_transformation(U1, U, U.basis())
sage: rhoC = linear_transformation(V1, V, V.basis())
\end{sageexample}
%
Now we experiment with a ``random'' element of the domain.  Study the third computation carefully, as it is the Sage version of \acronymref{theorem}{FTMR}.  You might compute some of the pieces of this expression to build up the final expression, and you might duplicate the experiment with a different input and/or with the \verb?T2? version.
%
\begin{sageexample}
sage: T_symbolic(-3, 4, -5, 2)
(6, 3, -3)
sage: u = vector(QQ, [-3, 4, -5, 2])
sage: T1(u)
(6, 3, -3)
sage: (rhoC^-1)(T1.matrix(side='right')*rhoB(u))
(6, 3, -3)
\end{sageexample}
%
One more time, but we will replace the vector representation linear transformations with Sage conveniences.  Recognize that the \verb?.linear_combination_of_basis()? method is the inverse of vector representation (it is ``un-coordinatization'').
%
\begin{sageexample}
sage: output_coord = T1.matrix(side='right')*U1.coordinate_vector(u)
sage: V1.linear_combination_of_basis(output_coord)
(6, 3, -3)
\end{sageexample}
%
We have concentrated here on the first version of the conclusion of \acronymref{theorem}{FTMR}.  You could experiment with the second version in a similar manner.  Extra credit: what changes do you need to make in any of these experiments if you remove the \verb?side='right'? option?
%
\begin{sageverbatim}
\end{sageverbatim}
%